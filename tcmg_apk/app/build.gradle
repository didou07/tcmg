plugins {
    id 'com.android.application'
}

android {
    namespace 'com.tcmg.app'
    compileSdk 35

    defaultConfig {
        applicationId "com.tcmg.app"
        minSdk 24
        targetSdk 35
        versionCode 3
        versionName "3.0.0"

        ndk {
            abiFilters 'arm64-v8a', 'armeabi-v7a', 'x86_64'
        }

        externalNativeBuild {
            cmake {
                arguments "-DANDROID_STL=none"
            }
        }
    }

    def keystorePropsFile = rootProject.file("keystore.properties")
    if (keystorePropsFile.exists()) {
        def keystoreProps = new Properties()
        keystoreProps.load(new FileInputStream(keystorePropsFile))
        signingConfigs {
            release {
                storeFile     file(keystoreProps['storeFile'])
                storePassword keystoreProps['storePassword']
                keyAlias      keystoreProps['keyAlias']
                keyPassword   keystoreProps['keyPassword']
            }
        }
    }

    externalNativeBuild {
        cmake {
            path "src/main/jni/CMakeLists.txt"
            version "3.22.1"
        }
    }

    buildFeatures {
        viewBinding true
    }

    buildTypes {
        debug   { versionNameSuffix "-debug" }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            if (keystorePropsFile.exists()) signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    lint {
        abortOnError false
    }

    packaging {
        jniLibs { useLegacyPackaging true }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.activity:activity:1.10.1'
    implementation 'androidx.fragment:fragment:1.8.6'
    implementation 'androidx.core:core:1.15.0'
}
